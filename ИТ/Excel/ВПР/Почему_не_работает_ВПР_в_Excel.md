---
title: Почему не работает ВПР в Excel?
description: 
published: true
date: 2024-11-01T13:45:43.690Z
tags: 
editor: markdown
dateCreated: 2024-10-31T10:29:27.180Z
---

[Функция ВПР](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_пошаговая_инструкция) – это очень мощный инструмент поиска. Но если он по каким-то причинам завершился неудачно, то вы получите сообщение об ошибке `#Н/Д` (`#N/A` в английском варианте).

Давайте постараемся вместе ответим на вопрос: «**Почему функция ВПР не работает?**»

В первую очередь, сообщение об ошибке вы увидите, если данные, которое вы ищете, действительно отсутствуют в указанном диапазоне поиска. Здесь мы бессильны.

Все остальные случаи связаны с тем, что мы что-то не учли или сделали неверно.

Итак, почему ВПР не находит значение, хотя оно есть?

![Почему не работает ВПР в Excel 01](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/01.png ""){.align-center}

# Неточность при вводе данных
В этом случае ничего найти не получится, и ВПР возвратит ошибку `#Н/Д`. Самое простое решение здесь очень часто оказывается самым верным. Быть может, вы просто ошиблись при вводе критерия поиска.

Это вполне может случиться, особенно если вы записываете его прямо в формулу.

# Опечатка при вводе функции
Если вы видите ошибку `#ИМЯ?`, то это означает, что при записи названия самой функции вы допустили неточность – перепутали или добавили лишнюю букву.

Проверьте синтаксис, и всё будет в порядке.

# Неверные ссылки
Проверьте, верно ли вы указали диапазон поиска, а также координаты значения, которое мы будем искать.

Случается, что в формуле вы указали [обычные (относительные) ссылки](/), забыв заменить их на [абсолютные ссылки (при помощи $)](/), а затем произвели какие-то действия с таблицей. К примеру, добавили столбец. Ваши ссылки в формуле теперь будут указывать на неверные координаты.

> **Важно!** При таком изменении ссылок вы можете и не знать, что найдено не то, что нужно. Ведь какое-то значение все же возвращено, хоть и неверное. Поэтому будьте внимательны в использовании относительных и абсолютных ссылок! Рекомендуем по этой причине стараться использовать «умные» таблицы или именованные диапазоны. {.is-warning}

# Неверно указан параметр «интервальный просмотр»
Включен поиск до первого приблизительного совпадения в отсортированном диапазоне (параметр = ИСТИНА или вовсе опущен), но на самом деле данные не отсортированы.

Об этой проблеме мы подробно говорили ранее.

# Столбец поиска не является первым слева столбцом диапазона поиска
Часто забывают о том, что ВПР ищет совпадающие данные только в первом слева столбце диапазона, который вы указали.

Если ваши просматриваемые данные находятся не в первом, а во втором или другом столбце, то она не сможет найти ни одного совпадения, и вы вновь увидите сообщение `#Н/Д`.

Это важное ограничение нельзя забывать.

# Несовпадение форматов данных
Формат ячейки, откуда берется искомое значение наименования (например D3 в нашем случае), и формат ячеек первого столбца (A3:A21) из диапазона поиска отличаются (например, числовой и текстовый). Этот случай особенно часто встречается при использовании вместо текстовых наименований числовых кодов (номера счетов, артикулы, идентификаторы, даты и т.п.). Ведь число, записанное в таблицу Excel, может быть в двух принципиально разных состояниях - как число и как текст. И визуально их отличить практически невозможно.

## Преобразуем число в текст

![Почему не работает ВПР в Excel 02](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/02.png ""){.align-center}

Как видите, с виду записи одинаковы, однако в D6 артикул сохранен как число, а в A10 – как текст (на таких ячейках обычно видна пометка - зелёный уголок). Текст не может быть равнозначен числу, поэтому получаем `#Н/Д`. ВПР не находит значение, хотя оно с виду есть.

Как решить эту проблему со случайным несоответствием в формате записи? Можно использовать функцию ТЕКСТ, которая преобразует любой тип данных в текстовый вид.

Используем формулу
> =ВПР(ТЕКСТ(D6;"#");\$A\$3:\$B\$21;2;ЛОЖЬ) {.is-success}

Как видно, обычную ссылку на D6 мы заменяем конструкцией

> ТЕКСТ(D6,"#") {.is-success}

Первый аргумент – это адрес, а второй аргумент означает формат, в который мы будем преобразовывать значение (в нашем случае – текстовый).

![Почему не работает ВПР в Excel 03](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/03.png ""){.align-center}

Можно поступить и проще - "приклеить" к числу пустую строку при помощи оператора склейки &.

> =ВПР(D6&"";\$A\$3:\$B\$21;2;ЛОЖЬ) {.is-success}

Запомните простое правило:
> Если какую-либо текстовую операцию мы пытаемся провести над ячейкой, в которой записано число, то Excel обязательно попытается превратить это число в текст. {.is-warning}

Если несовпадение форматов встречается редко, то можно просто исправить формат записи данных. Измените формат ячейки на текстовый и пересохраните ее содержание (F2 и затем ENTER). Содержимое будет преобразовано в текст.

Как видите, ошибка исчезла, поскольку теперь текстовое значение сравнивается с таким же текстом.

## Преобразуем текст в число
А если нужно сделать обратное преобразование? Преобразовать число как текст в обычное число, чтобы все данные в таблице были одного типа? Давайте рассмотрим на примере.

![Почему не работает ВПР в Excel 04](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/04.png ""){.align-center}

Добавьте перед проблемным значением двойной минус. Это означает - дважды умножить на минус 1. А еще со школы мы помним, что минус на минус дает плюс. В результате ничего не изменится. Но есть важный момент.

> Если какую-либо математическую операцию мы пытаемся провести над ячейкой, в которой записан текст, то Excel обязательно попытается преобразовать его в число, чтобы выполнить вычисления. Другие способы конвертации текста в числа вы пожете посмотреть здесь: [Как преобразовать текст в число.](/) {.is-warning}

Так же и в нашем случае. Текст преобразуется в число. Итак, наша формула выглядит следующим образом:

> =ВПР(--A2;\$D\$2:\$E\$8;2;0) {.is-success}

## Если числовой и текстовый форматы беспорядочно перемешаны?
А теперь - самый сложный случай. И здесь тоже есть простое решение.

![Почему не работает ВПР в Excel 05](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/05.png ""){.align-center}

Если поиск с преобразованием значения в число не удался, то будем искать его как текст. Объединяем оба варианта при помощи [функции ЕСЛИОШИБКА](/).

> =ЕСЛИОШИБКА(ВПР(--O2;\$R\$2:\$S\$8;2;0);ВПР(O2&"";\$R\$2:\$S\$8;2;0)) {.is-success}

В результате получаем немного громоздкий, но зато вполне универсальный вариант, который позволит избежать ошибок в формуле ВПР из-за несовпадения форматов значений.

# Лишние пробелы и непечатаемые знаки
Формула не работает, потому что в артикуле присутствуют пробелы или невидимые непечатаемые знаки (перенос строки и т.п.).

В нашем примере на картинке чуть выше формула ВПР возвратила ошибку, так как в D8 при вводе данных случайно были добавлены два пробела после слова. И, поскольку сравниваются символьные значения, поиск, естественно, завершился неудачно.

Такая неточность может встречаться довольно часто, и обнаружить её визуально практически невозможно. К примеру, если наименование состоит из 2 слов, то вполне возможно случайное появление лишнего пробела между этими словами.

Могут быть также случайно добавлены символы, невидимые на экране и на печати (табуляция, перевод строки и т.д.). Визуально определить такие неточности ввода вы не сможете.

В этом случае можно использовать [функцию СЖПРОБЕЛЫ (TRIM)](/) и ПЕЧСИМВ (CLEAN) для удаления лишних пробелов либо других невидимых символов. Вместо

> =ВПР(D8;\$A\$3:\$B\$21;2;ЛОЖЬ) {.is-success}

вводим

> =ВПР(СЖПРОБЕЛЫ(ПЕЧСИМВ(D8));\$A\$3:\$B\$21;2;ЛОЖЬ) {.is-success}

Функция СЖПРОБЕЛЫ убирает пробелы, а ПЕЧСИМВ удаляет все непечатаемые и невидимые символы.

Эту комбинацию функций рекомендуем применять всегда «на всякий случай», чтобы подстраховать себя от случайных неточностей ввода данных.

# Неправильно указан номер столбца
Иногда функция ВПР не срабатывает, потому что неправильно указан номер столбца, значение из которого вы хотите получить (третий аргумент).

Он не может быть меньше 1 и не может быть больше, чем количество столбцов в указанном для поиска диапазоне. Если он указан неверно, то [получим ошибку `#ЗНАЧ!`](/).

Когда вы видите такое, пересчитайте количество колонок в диапазоне, который вы указали, и убедитесь, что оно не меньше, чем номер, указанный третьим аргументом функции. Быть может, вы указали 4, а колонок всего 3.

И в любом случае – это число должно быть больше 1. Вряд ли кто-то из вас будет руками вводить 0 или -1, но если вы получаете его при помощи каких-то вычислений, вложенных в формулу, то здесь-то и может быть проблема.

# Неверная ссылка на данные из другой таблицы
Мы с вами говорили о том, что диапазон поиска может быть расположен в другой таблице или даже в другом файле Excel.

Если вы случайно допустили неточность при указании ссылки на эти данные, то увидите сообщение `#ССЫЛКА!`.

Это будет означать, что вы ссылаетесь на неверный диапазон, который Excel найти не может.

# После изменения таблицы функция ВПР перестала работать
Вы сделали все правильно и до тех пор, пока вы не вставили несколько строк или столбцов в вашу таблицу, все работало верно.

Теперь же появилась ошибка `#Н/Д` либо результаты стали явно неверны. В чем дело?

А дело всё в том, что вы не использовали абсолютные ссылки, не добавляли к адресам ячеек, строк и столбцов знак $. В результате после добавления (или удаления) строк или столбцов ваши ссылки изменились, и всё сломалось.

![Почему не работает ВПР в Excel 06](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/06.gif ""){.align-center}

Кроме того, в третьем аргументе вы указываете порядковый номер столбца, из которого нужно извлечь данные. Но если вы вставили дополнительные колонки, то и количество столбцов, и их порядковые номера изменятся. А у вас в формуле используются «старые» номера.

Постарайтесь отменить сделанные изменения, благо Excel хранит всю историю изменений вашей таблицы.

> **Важно!** Всегда используйте абсолютные ссылки, где это необходимо, чтобы застраховать себя от подобных неожиданных изменений. {.is-warning}

# Как убрать сообщение #Н/Д в ВПР?
Если функция не находит точного совпадения или искомого объекта просто нет в таблице, то она выдает сообщения, о которых мы говорили выше. С одной стороны, это хорошо, поскольку отрицательный результат- это тоже результат.

Однако, иногда это не очень удобно, т.к. такие сообщения потом порождают новые проблемы в расчете итогов, попадают в распечатку и т.д. Ведь если на эту ячейку ссылаются какие-то другие формулы, то они будут неправильно работать.

Можно легко перехватить ошибки и заменить их на любое другое подходящее нам значение (например, на ноль) с помощью [функции ЕСЛИОШИБКА](/).

Синтаксис её подобен ЕСЛИ. В качестве условия используется выражение, которое потенциально может возвратить сообщение об ошибке.

> =ЕСЛИОШИБКА([выражение];[значение_если_ошибка]) {.is-success}

Задача – заменить его числом или выражением, указанным во втором аргументе. Рассмотрим это на примере.

![Почему не работает ВПР в Excel 07](/ИТ/Excel/ВПР/Почему_не_работает_ВПР_в_Excel/07.png ""){.align-center}

В нашем случае маракуйя отсутствует в прайсе. Но вместо `#Н/Д` мы выведем ноль.

Вот как нужно будет преобразовать наши формулы, чтобы все выглядело красиво.

> =ЕСЛИОШИБКА(ВПР(A4;\$D\$2:\$E\$7;2;0);0) {.is-success}

Естественно, вместо нуля можно вписать «не найдено» либо любое другое сообщение, а можно просто поставить пробел. Но в случае, когда на основе этих ячеек затем производятся какие-то вычисления, ноль, возможно, будет предпочтительнее.

Надеемся, теперь вы сможете ответить на вопрос: «Почему не работает функция ВПР в Excel?».