---
title: Функция ВПР в Excel — 22 факта, которые нужно знать
description: 
published: true
date: 2024-10-31T12:58:34.818Z
tags: 
editor: markdown
dateCreated: 2024-10-31T08:06:13.528Z
---

В процессе работы в Excel часто возникает задача извлечения нужных данных из рабочих таблиц. Для этой цели в Excel предусмотрена [функция ВПР (VLOOKUP в английском варианте)](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_пошаговая_инструкция). И хотя ВПР относительно проста в использовании даже для начинающих, есть много вещей, которые могут здесь пойти не так.

Одна из причин заключается в том, что имеется существенный недостаток: по умолчанию предполагается, что вам достаточно не точного, а приблизительного соответствия при поиске. Что, скорее всего, не соответствует вашим пожеланиям. Это может привести к тому, что результаты расчетов выглядят совершенно нормально, даже если они совершенно ошибочны. Поверьте, это НЕ то, что вы хотите объяснить своему боссу, после того, как он уже отправил вашу таблицу руководству :)

Читайте далее и узнайте, как справиться с этой задачей. Прочтите другие советы по оптимизации работы. Особое внимание мы уделим [ошибкам, возникающим при использовании формулы ВПР](/), а также приёмам, которые позволят сделать её использование простым и вместе с тем эффективным. Также мы разберём несколько случаев выбора и поиска данных, в которых продемонстрированы альтернативные методики, позволяющие существенно расширить стандартные возможности Excel.

Итак, какие особенности использования функции ВПР в Excel нужно учитывать?

# 1. Как расшифровывается ВПР в Excel?
Запомнить назначение формулы несложно: ВПР (VLOOKUP) переводится как сокращение «**В**ертикальный **ПР**осмотр» или на английском — “**V**ertical **L**ook **U**p”.

Такая расшифровка аббревиатуры ВПР указывает, что она последовательно просматривает содержимое определенного диапазона по вертикали и ищет нужное нам значение. Это одна из самых часто используемых функций отбора данных.

Термин «вертикальный», означает, что показатели в таблице должны быть расположены вертикально, а данные - по строкам. А для горизонтально структурированных данных используется ГПР (HLOOKUP на английском). Как вы, думаю, догадываетесь, ГПР расшифровывается как "горизонтальный просмотр". В общем, то же самое действие, но по горизонтали.

# 2. Что делает ВПР?
Она возвращает значение из таблицы как результат поиска другого, связанного с ним значения этой же таблицы.

Для этого она находит интересующее нас содержимое в первом столбце и возвращает (то есть показывает нам в ответ на наш запрос) соответствующие данные из ячейки, находящейся на пересечении строки с найденным значением и указанного нами столбца.

Необходимо, чтобы таблица была создана таким образом, чтобы значения поиска располагались в крайнем левом столбце. Скажем, названия товаров. Данные, которые вы хотите получить (результирующие значения), могут быть записаны в любом месте, находящемся правее. А правее в этой таблице могут значиться артикул товара, его вес, цена, количество и т.д. Мы движемся по первому столбцу сверху вниз, и как только находим что-то подходящее, то останавливаемся на этой строке и берем данные из какого-то из столбцов, находящихся правее. Нужно только указать, из какого по счету.

# 3. Для чего используется ВПР?
Эти действия подобны розыску нужного номера в телефонном справочнике. Для этого вы сначала листаете справочник, чтобы обнаружить искомую фамилию и, только обнаружив её, получаете номер телефона. Или же вы просматриваете прайс-лист, и напротив нужного товара видите его цену.

Таким образом, ВПР нужна, чтобы найти в Экселе определенную информацию в одной таблице и вставить её в определенное место в другую.

*Ещё раз сделаем важное замечание: поиск всегда происходит в первом (крайнем левом) столбце.*

Именно это ограничение и не позволяет считать её универсальным решением. Но все же возможности применения очень широки.

Синтаксис ВПР позволят нам применять её для очень большого круга задач, при котором необходимо найти и вернуть определённое значение.

Мы начнем с самого простого и постепенно будем рассматривать все более сложные способы  её использования.

# 4. Как расшифровываются параметры ВПР?
Чтобы лучше понять, что такое ВПР в Excel, давайте для начала просто попробуем создать её на нашем рабочем листе.

Устанавливаем курсор в нужную ячейку, в которую нужно вставить цену ➁ из прайс-листа ➀ по определенному товару. Нажимаем на значок возле строки формул и в категории «Ссылки и массивы» находим ВПР.

![Функция ВПР в Excel 22 факта 01](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/01.png "Как расшифровываются параметры ВПР?" =60%x){.align-center}

Либо просто ставим «=» и начинаем писать название. И  тут же появляется выпадающий список подходящих функций. Выбираем нужную.

Затем в открывшемся окне заполняем все параметры. Их вы видите на рисунке выше.

> =ВПР(искомое; таблица; номер_столбца; параметр_просмотра) {.is-success}

- Искомое — то, что ищем.
- Таблица — диапазон ячеек, в котором ищем и из которого затем извлекаем данные.
- Номер_столбца — номер столбца диапазона, из которого возвращаются данные, если значение в первом столбце данной строки совпадает с аргументом искомое_ значение.
- Интервальный_просмотр — необязательный, но очень важный логический аргумент. О нем мы поговорим ниже.

# 5. ВПР имеет два режима поиска
Четвертый параметр «интервальный просмотр» позволяет задать режим сравнения, в котором нужно работать – точный или приблизительный. Это логический параметр, то есть для него возможно 2 варианта:

**0** или **ЛОЖЬ** – интервальный просмотр **выключен** (ищем точное соответствие)
**1** или **ИСТИНА** – интервальный просмотр **включен** (достаточно приблизительного)
Если этот параметр - ЛОЖЬ, то ищется точное равенство в первом столбце с аргументом «искомое». Если же такого нет, воз­вращается ошибка `#Н/Д`. Точный режим нужен, когда вы хотите найти информацию, основанную на каком-то уникальном ключе. Скажем, информацию о товаре - по коду товара, или данные фильма по его названию, человека - по фамилии.

Если он равен ИСТИНА или вовсе пропущен, то допускается неточное совпадение в данных _отсортированной_ таблицы с аргументом «искомое». То есть нам нужно «лучшее соответствие», «как можно самое близкое».  Возможно, вы просматриваете почтовые тарифы по весу посылки, ищете налоговую ставку на основе дохода, или определяете скидку на основе ежемесячного объема продаж. В этих случаях вы, скорее всего, не найдете именно ваш вес либо точную цифру вашего дохода.  А если точного нет, то принимается ближайшее подходящее значение.

Обратите внимание, что при этом предполагается, что наш перечень отсортирован по столбцу поиска.

# 6. Внимание: неточный поиск включён по умолчанию!
К сожалению, четвертый аргумент является необязательным и по умолчанию имеет значение ИСТИНА. Это означает, что программа ищет не именно ваше, а просто похожее значение по умолчанию, даже если вы указали только 3 аргумента.

Если аргумент используется ЛОЖЬ (или ноль), то сортировка не обязательна, поскольку ищется точное соответствие.

Это часто создает проблемы, потому что многие люди невольно оставляют ВПР в режиме по умолчанию, забывают точно указать, как именно они хотят искать. А это может привести к неверному результату, если в данных не было сортировки. То есть, программа обнаружит первое подходящее значение и прекратит поиск. Допустим, мы ищем "апельсин", а первым в списке числится "банан". Эксель решит, что после слова на букву Б нет смысла искать апельсин, ведь список должен быть отсортирован по алфавиту. А если вы не сделали сортировку, то ваш апельсин вполне может находиться где-то ниже в списке. Но найти его уже не удастся. Результат - ошибка в заполнении таблицы с товарами и ценами.

Чтобы избежать этой проблемы, обязательно используйте ЛОЖЬ или ноль в качестве 4-го аргумента, когда нужен именно точный поиск.

В подавляющем большинстве случаев используется точное сравнение: если в прайс-листе найдется точно такое же название товара, то программа выведет его цену. В противном случае мы получим ошибку `#Н/Д`.

Вы спросите – а зачем же тогда этот параметр, если его значение ИСТИНА (TRUE) или же отсутствие приводит к таким проблемам? Ответ заключается в том, что если всё же вы будете применять её на отсортированном массиве, то производительность и скорость вычислений возрастут по разным оценкам где-то в 50 (пятьдесят!) раз. При работе с большими объемами данных это будет очень заметно.

Поэтому, если есть такая возможность, старайтесь сортировать большие массивы данных. Если делать всё без ошибок, то производительность Excel вырастет многократно.

> Поэтому мы рекомендуем всегда устанавливать 4-й аргумент явным образом, даже если в конкретной ситуации этого не требуется. Таким образом, у вас всегда есть визуальное напоминание о режиме поиска, который вы используете.

# 7. Для приблизительного поиска данные должны быть отсортированы
Если аргумент интервальный_просмотр равен ИСТИНА или опущен, то для правильной работы данные должны быть упорядоченны по возрастанию. Сортировка производится по тому столбцу, в котором ищем, то есть по первому. Если сортировку не сделать, то как только будет найдено значение большее, чем искомое, то процесс будет прекращен, несмотря на то, что ответ будет находиться чуть ниже.

Если ваш критерий начинается с буквы “A” (апельсин), а в начале списка находится слово, начинающееся с буквы “C” (допустим, сливы), то, оценив это, Эксель решит, что если встретилась буква “C”, то в списке букву “A” дальше искать бессмысленно. Работа остановится и будет возвращена ошибка `#Н/Д` (`#N/A` в англоязычной версии), несмотря на то, что правильное наименование в вашем списке было, но чуть ниже. Но вы об этом даже не узнаете.

Может случиться и другое – будет найден товар с похожим названием, сотрудник с похожей фамилией. В случае, если вы ищете конкретного человека либо конкретный товар, вряд ли вас устроит такой приблизительный поиск. Но самое плохое заключается в том, что вы не узнаете о том, что найдено просто первое похожее. А это может привести к ошибкам в принятии решений на основе ваших расчетов.

**Также имейте в виду, что иногда текстовые данные могут выглядеть отсортированными, даже если это не так.** Ведь в строке могут встречаться пробелы в конце, буквы русского алфавита заменены аналогичными по написанию английскими и т.д. Визуально вы определить это вряд ли сможете. Мы расскажем вам в отдельной статье, как решить эту проблему.

# 8. ВПР ищет только справа
Возможно, самым большим ограничением ВПР является то, что она может искать только вправо от ключевого столбца.

Это означает, что вы сможете получить данные только из колонок, находящихся справа от первого столбца. Когда значения поиска находятся в первом (крайнем левом) столбце, это ограничение не имеет большого значения, поскольку все остальные уже находятся справа. Однако, если область поиска находится внутри таблицы, вы сможете искать значения только справа от неё. Вам также нужно будет в качестве источника данных брать не всю таблицу, а только ее часть, которая начинается с области поиска.

Впрочем, это ограничение можно преодолеть, о чем мы также вам расскажем. Читайте подробнее: [4 способа, как сделать левый ВПР в Excel](/).

# 9. ВПР пригодится, чтобы объединять данные из разных таблиц
Типичный случай использования этого замечательного инструмента - объединение данных из двух или более таблиц. Предположим, у вас есть данные заказа в одной, а информация о цене - в другой, и вы хотите перенести цену товаров в таблицу заказов для расчета их точной стоимости.

Поскольку наименование товара имеется в обеих таблицах, то вы можете использовать его в качестве ключа поиска, чтобы получить нужную цену.

![Функция ВПР в Excel 22 факта 02](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/02.png "ВПР пригодится, чтобы объединять данные из разных таблиц" =70%x){.align-center}

Обратите также внимание, что даже точный поиск можно сделать немного приблизительным, используя знаки подстановки * и ?. Напомню, что знак вопроса заменяет собой любой один символ, а звёздочка - любое количество знаков (в том числе и ноль). Таким вот образом мы обнаружили в перечне товаров персики, записав в условии поиска **"\*"&"персики"&"\*"** или же **"\*"&D2&"\*"**.

# 10. Может помочь классифицировать данные
Если вам когда-либо понадобится применить классифицировать данные, то есть разделить их на группы по каким-то признакам, вы можете легко сделать это, используя таблицу, которая выступает в качестве «ключа» для назначения этих признаков.

Классическим случаем являются оценки, где вам нужно определить результат на основе набранных баллов:

![Функция ВПР в Excel 22 факта 03](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/03.png "Может помочь классифицировать данные" =70%x){.align-center}

В этом случае используется приблизительное соответствие, поэтому важно, чтобы информация сортировалась в порядке возрастания.

# 11. Абсолютные ссылки облегчают и ускоряют работу с ВПР
В ситуациях, когда вы хотите получать информацию из более чем одной колонки или если вам нужно скопировать и вставить формулу, вы можете сэкономить время и упростить работу. [Используйте абсолютные ссылки](/) для указания диапазона поиска и массива данных. Это позволяет затем просто скопировать её, а потом, если нужно, изменить только номер колонки, чтобы извлечь из таблицы другую информацию.

Поскольку ссылки на значение поиска и массив данных являются абсолютными, мы можем скопировать в соседние ячейки, а затем вернуться и изменить номер по мере необходимости. Вы это видите на рисунке ниже.

![Функция ВПР в Excel 22 факта 04](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/04.png "Абсолютные ссылки облегчают и ускоряют работу с ВПР" =70%x){.align-center}

# 12. Именованные диапазоны облегчают понимание формул и ещё больше упрощают работу
Абсолютные ссылки выглядят довольно некрасиво и не очень понятно и наглядно. Поэтому можно сделать ваши расчёты намного чище и проще для понимания, [заменив абсолютные ссылки именованными диапазонами](/). И никакие возможные изменения на вашем листе Excel не смогут их «испортить».

Копировать и переносить их также можно без проблем.

В приведенном выше примере с данными о сотрудниках вы можете назвать входную ячейку B2 «фамилия», а затем выделить все ячейки с информацией и назвать диапазон B5:F100 как «ДанныеСлужащего». Затем перепишите свою формулу в C2 следующим образом:

> =ВПР(фамилия;ДанныеСлужащего;2;ЛОЖЬ) {.is-success}

![Функция ВПР в Excel 22 факта 05](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/05.png "Именованные диапазоны облегчают понимание формул и ещё больше упрощают работу" =70%x){.align-center}

Сравните сами - насколько понятнее стал расчет из совета №12 по сравнению с №11.

# 13. Вставка столбца может «сломать» ваши вычисления
Если вы вставите дополнительную колонку внутрь таблицы, из которой вы извлекаете данные, то ваши расчеты могут «сломаться». Дело в том, что в результате изменятся порядковые номера некоторых колонок с данными. А вы по-прежнему ссылаетесь на старые номера. В результате извлекать данные вы уже будете не из тех адресов.

![Функция ВПР в Excel 22 факта 06](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/06.gif "Вставка столбца может «сломать» ваши вычисления" =70%x){.align-center}

Здесь вы видите результаты для отдела, когда код сотрудника и даты стали неверными, когда был вставлен новый столбец D между именем и отделом. Имя же человека по-прежнему было взято правильное, потому что находится слева от места вставки.

Чтобы избежать этой проблемы, вы можете рассчитать и использовать меняющийся индекс столбца, как описано в следующих двух советах.

# 14. Индекс столбца можно рассчитать автоматически
Вы можете использовать функцию СТОЛБЕЦ() для генерации динамических индексов. Если вы получаете данные из последовательных колонок, этот трюк позволяет настроить одну первую формулу ВПР, а затем просто скопировать ее, не меняя ничего руками.

Посмотрите, как мы можем использовать функцию **СТОЛБЕЦ** (COLUMN в английском варианте) для создания динамического индекса. Для первой формулы в ячейке C3 нам нужен номер столбца 2. Поэтому запишем:

> =ВПР(\$B\$2;\$B\$5:\$G\$100;СТОЛБЕЦ()-1;ЛОЖЬ) {.is-success}

Поскольку столбец C является третьим на листе, поэтому нам просто нужно вычесть 1 и получить 2.

![Функция ВПР в Excel 22 факта 07](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/07.gif "Индекс столбца можно рассчитать автоматически" =70%x){.align-center}

Далее просто скопируйте из C3 в D3, E3, F3.

# 15. Используйте ВПР + ПОИСКПОЗ для полностью динамического индекса столбца
Усовершенствовав предыдущий совет, вы можете использовать [функцию ПОИСКПОЗ](/) для определения позиции столбца и возврата полностью динамического его индекса (порядкового номера).

Иногда это называется **двусторонним поиском**, поскольку вы просматриваете как по вертикали, так и по горизонтали.

Пригодиться это может при вычислении продаж для продавца в конкретном месяце или выборе цены на конкретный продукт от конкретного поставщика.

Предположим, что у вас есть продажи за месяц, с разбивкой по менеджерам.

ВПР может легко найти нужного сотрудника, но у нее нет возможности автоматически обрабатывать еще и название месяца. Хитрость заключается в том, чтобы использовать функцию ПОИСКПОЗ вместо статического индекса. С ее помощью мы вычислим номер нужной нам колонки.

При этом давайте еще попробуем использовать именованные диапазоны. Так нам будет проще разобраться.

Итак, A7:D17 присвоим имя «данные». A6:D6 назовем «месяц».

В результете в G8 мы можем записать:

> =ВПР(G6;данные;ПОИСКПОЗ(G7;месяц;0);0) {.is-success}

![Функция ВПР в Excel 22 факта 08](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/08.png "Используйте ВПР + ПОИСКПОЗ для полностью динамического индекса столбца" =70%x){.align-center}

Примечание: подобный двусторонний поиск с помощью ИНДЕКС и ПОИСКПОЗ, который предлагает большую гибкость и лучшую производительность для больших наборов данных, мы рассмотрим в дальнейшем: [Вычисление номера столбца для извлечения данных в ВПР](/). 

# 16. Можно использовать символы подстановки для определения частичного соответствия
Каждый раз, когда вы используете ВПР в режиме точного поиска, у вас есть возможность использовать подстановочные знаки в поисковом значении. Это может показаться нелогичным, но эти знаки позволяют найти точное совпадение на основе частичного совпадения :)

Если аргумент «искомое» является текстом и **интервальный_просмотр** имеет значение ЛОЖЬ, то для обнаружения точного совпадения можно использовать символы подстановки * и ?.

- \* (звездочка) – любое количество любых символов (в том числе и их полное отсутствие)
- ? (вопросительный знак) – один любой символ.
Но будьте внимательны с этими знаками. Как только будет найдено что-то подходящее, дальше искать уже не будет.

![Функция ВПР в Excel 22 факта 09](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/09.png "Можно использовать символы подстановки для определения частичного соответствия" =70%x){.align-center}

*Они дают вам простой способ создать «ленивый поиск», но они также позволяют ошибиться.*

# 17. Вместо ошибки #Н/Д можно показать любое сообщение
В режиме точного совпадения ВПР отобразит ошибку `#Н/Д`, если ничего не найдено. С одной стороны, это полезно, потому что оно однозначно говорит вам, что нет подходящих данных. 

Иногда это не очень удобно, поскольку такие ошибки `#Н/Д` потом порождают новые ошибки при попытке рассчитать сумму, попадают в распечатку и т.д. Можно легко перехватить их и заменить на любое другое подходящее значение (скажем, на ноль) с помощью [функции ЕСЛИОШИБКА (IFERROR)](/).

![Функция ВПР в Excel 22 факта 10](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/10.png "Вместо ошибки #Н/Д можно показать любое сообщение" =70%x){.align-center}

Далее мы рассмотрим обработку ошибок более подробно. Читайте: [Почему не работает ВПР?](/).

# 18. Числа, записанные как текст, могут стать причиной ошибки
Иногда таблица, с которой вы работаете, может содержать числа, введенные в виде текста.  И если вы ищете именно число, то совпадений не обнаружите.

![Функция ВПР в Excel 22 факта 11](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/11.png "Числа, записанные как текст, могут стать причиной ошибки" =70%x){.align-center}

В этом случае артикул 99023 записан в одном случае как текст, в другом – как число. Поэтому и видим ошибку `#Н/Д`.

Чтобы решить эту проблему, необходимо убедиться, что критерий поиска и первая колонка имеют одинаковый тип данных (либо оба числа, либо оба текста).

Один из способов сделать это - преобразовать значения в диапазоне поиска в числа. Простой способ сделать это - прибавить ноль к каждому из них с помощью инструмента специальной вставки.

- [Читайте подробнее *как преобразовать текст в число*.](/)
{.links-list}

И наоборот, можно преобразовать критерий поиска в текст, присоединяя пустой пробел ("") следующим образом:

> =ВПР(D6&"",A3:A21,2,0) {.is-success}

Если вы не уверены, когда у вас будут цифры, а когда будет текст, вы можете использовать оба варианта, заключив ВПР в ЕСЛИОШИБКА и написав выражение для обработки обоих случаев:

> =ЕСЛИОШИБКА(ВПР(D6,A3:A21,2,0);ВПР(D6&"",A3:A21,2,0)) {.is-success}

А вообще об ошибках мы будем говорить в отдельной статье: [ВПР с функцией ЕСЛИОШИБКА](/).

# 19. ВПР нужна для замены вложенных операторов ЕСЛИ
Одним из наиболее интересных применений ВПР является замена вложенных операторов ЕСЛИ. Наверняка вы когда-либо создавали серию вложенных как матрешки друг в друга условий и заметили, что они работают нормально, но требуют большого количества скобок. Запутаться и сделать логическую ошибку тут очень легко.

Обычное использование вложенных ЕСЛИ - это расчет процента скидки или уровня налоговой ставки на основе соответствующей шкалы.

В приведенном ниже скриншоте вы можете сравнить использование [вложенной формулы ЕСЛИ](/) и то, что можно сделать при помощи функции ВПР.

![Функция ВПР в Excel 22 факта 12](/ИТ/Excel/ВПР/Функция_ВПР_в_Excel_22_факта/12.png "ВПР нужна для замены вложенных операторов ЕСЛИ" =70%x){.align-center}

Полная вложенная функция ЕСЛИ выглядит следующим образом:

> =ЕСЛИ(B1>50000,01;20%;ЕСЛИ(B1>30000,01;17%;ЕСЛИ(B1>20000,01;15%; ЕСЛИ(B1>10000,01;13%;ЕСЛИ(B1>2000,01;12%;10%))))) {.is-success}

Это прекрасно работает, но учтите, что логика и шкала оценки вставляются прямо в выражение. Если шкала по какой-либо причине изменится, вам нужно будет аккуратно изменить его, а затем, возможно, скопировать куда необходимо.

С ВПР все гораздо проще. Все, что вам нужно сделать, это убедиться, что список интервалов оценки правильно настроен, то есть отсортирован в порядке возрастания.

> =ВПР(B1;D2:F7;3;1) {.is-success}

Обратите внимание, что используется интервальный (неточный) поиск.

Приятным бонусом этого подхода является то, что логика и оценки записаны прямо на лист Excel. Если что-то изменится, вы можете просто поправить критери границ дохода, а редактирование самого выражения не требуется.

# 20. Можно использовать только один критерий
ВПР может искать только на основе одного критерия, который указывается в качестве значения для поиска в первом столбце (предназначенном для поиска).

Это означает, что вы не можете легко сделать такие вещи, как поиск сотрудника с фамилией «Петров» в «Бухгалтерии» или поиск сотрудника на основе имени и фамилии, если они записаны в отдельных столбиках.

Однако существуют способы преодоления этого ограничения. Одним из простых обходных путей является создание вспомогательного столбца, который объединяет значения из разных колонок для создания новых значений поиска. 

Об этом и других способах борьбы с этим ограничением читайте на нашем сайте в специальных инструкциях: [**ВПР** с несколькими условиями — 5 примеров](/) и [Поиск ВПР нескольких значений по нескольким условиям](/).

# 21. Два ВПР быстрее, чем один
Это может показаться совершенно невероятным, но когда у вас большой набор данных и вам необходимо найти точное совпадение, вы можете значительно ускорить вычисления, добавив ещё один ВПР в формулу!

Предыстория: представьте, что у вас много данных о заказах, скажем, более 10000 записей, и ВПР нужна для поиска общей суммы заказа на основе идентификатора заказа. Итак, вы используете что-то вроде этого:

> =ВПР(заказ_ID, заказ_данные, 5,ЛОЖЬ) {.is-success}

То есть, мы ищем точное совпадение. Если номер заказа не будет найден, то получим ошибку `#Н/Д`.

Проблема в том, что точные совпадения действительно ищутся медленно, потому что Excel должен перебирать последовательно через все значения, пока не найдет искомое (или нет).

И наоборот, приблизительные совпадения молниеносны, потому что Excel может выполнять то, что называется бинарным поиском.

Однако проблема с этим двоичным поиском состоит в том, что Excel может возвращать неправильный результат, когда номер заказа не найден. Он просто возьмет наиболее похожий и близкий, что вряд ли вас устроит. Хуже того, ответ может выглядеть совершенно нормально, поэтому ошибку очень сложно обнаружить.

Решение состоит в том, чтобы использовать ВПР дважды, оба раза в режиме приблизительного соответствия. Первый раз просто проверяем, что значение действительно существует. Если это так, второй ВПР запускается (опять же, в режиме приблизительного соответствия), чтобы получить нужные данные. Если нет, вы можете вернуть любую фразу, которой вы хотите указать, что результат не был найден.

Окончательное выражение выглядит так:

> =ЕСЛИ(ВПР(заказ_ID, заказ_данные, 1,ИСТИНА)=заказ_ID; ВПР(заказ_ID,заказ_данные,5,ИСТИНА);"Не найдено") {.is-success}

То есть, мы сначала ищем и извлекаем номер заказа. И если результат этого поиска точно совпадает с критерием, повторяем ещё раз, но теперь уже извлекаем именно сумму заказа.

*Примечание: ваши данные должны быть отсортированы, чтобы использовать этот способ.*

# 22. ИНДЕКС и ПОИСКПОЗ могут больше и лучше, чем ВПР
Если вы будете читать о работе в Excel, то вы, вероятно, столкнётесь с дебатами «**ВПР или ИНДЕКС+ПОИСКПОЗ?**». Спор может быть на удивление горячим :)

Суть в следующем: [ИНДЕКС+ПОИСКПОЗ](/) может делать все, что может делать ВПР (и ГПР), с гораздо большей гибкостью, но за счет немного большей сложности. 

Таким образом, те, кто поддерживает ИНДЕКС и ПОИСКПОЗ, будут утверждать (и очень разумно), что вы могли бы также начать их изучать, так как в итоге это дает вам лучший набор инструментов.

Аргумент против заключается в том, что здесь требуется две функции вместо одной, поэтому пользователям (особенно начинающим) сложнее учиться и осваивать их.

Мой совет состоит в том, что если вы часто используете Excel, то обязательно нужно научиться использовать ИНДЕКС и ПОИСКПОЗ. Это очень мощная комбинация.

Но я также думаю, что вы должны обязательно знать, что такое ВПР. Ведь с ней вы столкнётесь повсюду, часто в файлах, которые вы получили от других людей. В простых ситуациях она справится без проблем.